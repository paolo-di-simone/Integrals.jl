{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "b40dc6fd",
   "metadata": {},
   "source": [
    "# LAR integrals"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "fe4a142a",
   "metadata": {},
   "source": [
    "### API:\n",
    "* *function M(alpha::Int, beta::Int)::Float64*\n",
    "* *function TT(tau::Array{Float64,2}, alpha::Int, beta::Int, gamma::Int, signedInt::Bool=false)*\n",
    "* *function II(P::LAR, alpha::Int, beta::Int, gamma::Int, signedInt=false)::Float64*\n",
    "* *function III(P::LAR, alpha::Int, beta::Int, gamma::Int)::Float64*\n",
    "* *function surface(P::LAR, signedInt::Bool=false)::Float64*\n",
    "* *function volume(P::LAR)::Float64*\n",
    "* *function firstMoment(P::LAR)::Array{Float64,1}*\n",
    "* *function secondMoment(P::LAR)::Array{Float64,1}*\n",
    "* *function inertiaProduct(P::LAR)::Array{Float64,1}*\n",
    "* *function centroid(P::LAR)::Array{Float64,1}*\n",
    "* *function inertiaMoment(P::LAR)::Array{Float64,1}*\n",
    "* *function chainAreas(V::Array{Float64,2},EV::Array{Int64,2},chains::Array{Int64,2})*\n",
    "* *function chainAreas(V::Array{Float64,2}, EV::Array{Int64,2}, chains::Array{Array{Int64,1},1})*"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d7c7362b",
   "metadata": {},
   "outputs": [],
   "source": [
    "using LinearAlgebra"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6aa9dde3",
   "metadata": {},
   "outputs": [],
   "source": [
    "const Points = Matrix\n",
    "const Cells = Array{Array{Int,1},1}\n",
    "const LAR = Union{ Tuple{Points, Cells},Tuple{Points, Cells, Cells} }"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "297b77e6",
   "metadata": {},
   "outputs": [],
   "source": [
    "function M(alpha::Int, beta::Int)::Float64\n",
    "    a = 0\n",
    "    for l=0:(alpha + 1)\n",
    "        a += binomial(alpha+1,l) * (-1)^l/(l+beta+1)\n",
    "    end\n",
    "    return a/(alpha + 1)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "55c2eab3",
   "metadata": {},
   "source": [
    "Funzione principale che effettua l'integrale su di un singolo triangolo"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2bdbdca4",
   "metadata": {},
   "outputs": [],
   "source": [
    "function TT(tau::Array{Float64,2}, alpha::Int, beta::Int, gamma::Int, signedInt::Bool=false)\n",
    "    vo,va,vb = tau[:,1],tau[:,2],tau[:,3]\n",
    "    a = va - vo\n",
    "    b = vb - vo\n",
    "    s1 = 0.0\n",
    "    for h=0:alpha\n",
    "        for k=0:beta\n",
    "            for m=0:gamma\n",
    "                s2 = 0.0\n",
    "                for i=0:h \n",
    "                    s3 = 0.0\n",
    "                    for j=0:k\n",
    "                        s4 = 0.0\n",
    "                        for l=0:m\n",
    "                            s4 += binomial(m,l) * a[3]^(m-l) * b[3]^l * M(h+k+m-i-j-l, i+j+l)\n",
    "                        end\n",
    "                        s3 += binomial(k,j) * a[2]^(k-j) * b[2]^j * s4\n",
    "                    end\n",
    "                    s2 += binomial(h,i) * a[1]^(h-i) * b[1]^i * s3;\n",
    "                end\n",
    "                s1 += binomial(alpha,h) * binomial(beta,k) * binomial(gamma,m) * vo[1]^(alpha-h) * vo[2]^(beta-k) * vo[3]^(gamma-m) * s2\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    c = cross(a,b)\n",
    "    if signedInt == true\n",
    "        return s1 * norm(c) * sign(c[3])\n",
    "    else\n",
    "        return s1 * norm(c)\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0409d96d",
   "metadata": {},
   "source": [
    "Funzione che permette di calcolare l'integrale di superficie di un polinomio del tipo: $II = \\int\\int_S x^\\alpha y^\\beta z^\\gamma dS$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f409b389",
   "metadata": {},
   "outputs": [],
   "source": [
    "function II(P::LAR, alpha::Int, beta::Int, gamma::Int, signedInt=false)::Float64\n",
    "    w = 0\n",
    "    V, FV = P\n",
    "    if typeof(FV) == Array{Int64,2}\n",
    "        FV = [FV[:,k] for k=1:size(FV,2)]\n",
    "    end\n",
    "    for i=1:length(FV)\n",
    "        tau = hcat([V[:,v] for v in FV[i]]...)\n",
    "        if size(tau,2) == 3\n",
    "            term = TT(tau, alpha, beta, gamma, signedInt)\n",
    "            if signedInt\n",
    "                w += term\n",
    "            else\n",
    "                w += abs(term)\n",
    "            end\n",
    "        elseif size(tau,2) > 3\n",
    "            println(\"ERROR: FV[$(i)] is not a triangle\")\n",
    "        else\n",
    "            println(\"ERROR: FV[$(i)] is degenerate\")\n",
    "        end\n",
    "    end    \n",
    "    return w\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "efa81741",
   "metadata": {},
   "source": [
    "Funzione che permette di calcolare l'integrale di volume di un polinomio del tipo: $III = \\int\\int\\int_V x^\\alpha y^\\beta z^\\gamma dV$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "00cf34a4",
   "metadata": {},
   "outputs": [],
   "source": [
    "function III(P::LAR, alpha::Int, beta::Int, gamma::Int)::Float64\n",
    "    w = 0\n",
    "    V, FV = P\n",
    "    for i=1:length(FV)\n",
    "        tau = hcat([V[:,v] for v in FV[i]]...)\n",
    "        vo,va,vb = tau[:,1],tau[:,2],tau[:,3]\n",
    "        a = va - vo\n",
    "        b = vb - vo\n",
    "        c = cross(a,b)\n",
    "        w += c[1]/norm(c) * TT(tau, alpha+1, beta, gamma)\n",
    "    end\n",
    "    return w/(alpha + 1)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "50fcbb18",
   "metadata": {},
   "outputs": [],
   "source": [
    "function surface(P::LAR, signedInt::Bool=false)::Float64\n",
    "    return II(P, 0, 0, 0, signedInt)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f6fdb942",
   "metadata": {},
   "outputs": [],
   "source": [
    "function volume(P::LAR)::Float64\n",
    "    return III(P, 0, 0, 0)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6484f017",
   "metadata": {},
   "outputs": [],
   "source": [
    "function firstMoment(P::LAR)::Array{Float64,1}\n",
    "    out = zeros(3)\n",
    "    out[1] = III(P, 1, 0, 0)\n",
    "    out[2] = III(P, 0, 1, 0)\n",
    "    out[3] = III(P, 0, 0, 1)\n",
    "    return out\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b97c45d1",
   "metadata": {},
   "outputs": [],
   "source": [
    "function secondMoment(P::LAR)::Array{Float64,1}\n",
    "    out = zeros(3)\n",
    "    out[1] = III(P, 2, 0, 0)\n",
    "    out[2] = III(P, 0, 2, 0)\n",
    "    out[3] = III(P, 0, 0, 2)\n",
    "    return out\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "43806666",
   "metadata": {},
   "outputs": [],
   "source": [
    "function inertiaProduct(P::LAR)::Array{Float64,1}\n",
    "    out = zeros(3)\n",
    "    out[1] = III(P, 0, 1, 1)\n",
    "    out[2] = III(P, 1, 0, 1)\n",
    "    out[3] = III(P, 1, 1, 0)\n",
    "    return out\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "829dd664",
   "metadata": {},
   "outputs": [],
   "source": [
    "function centroid(P::LAR)::Array{Float64,1}\n",
    "    return firstMoment(P)./volume(P)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "cf1a1ff2",
   "metadata": {},
   "outputs": [],
   "source": [
    "function inertiaMoment(P::LAR)::Array{Float64,1}\n",
    "    out = zeros(3)\n",
    "    result = secondMoment(P)\n",
    "    out[1] = result[2] + result[3]\n",
    "    out[2] = result[3] + result[1]\n",
    "    out[3] = result[1] + result[2]\n",
    "    return out\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "39d15140",
   "metadata": {},
   "outputs": [],
   "source": [
    "function chainAreas(V::Array{Float64,2},EV::Array{Int64,2},chains::Array{Int64,2})\n",
    "    FE = [chains[:,f] for f=1:size(chains,2)]\n",
    "    return chainAreas(V,EV,FE)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "100f6840",
   "metadata": {},
   "outputs": [],
   "source": [
    "function chainAreas(V::Array{Float64,2}, EV::Array{Int64,2}, chains::Array{Array{Int64,1},1})\n",
    "    if size(V,1) == 2\n",
    "        V = vcat(V,zeros(1,size(V,2)))\n",
    "    end\n",
    "    pivots = [EV[:,abs(chain[1])][1] for chain in chains]\n",
    "    out = zeros(length(pivots))\n",
    "    for k=1:length(chains)\n",
    "        area = 0\n",
    "        triangles = [[] for h=1:length(chains[k])]\n",
    "        for h=1:length(chains[k])\n",
    "            edge = chains[k][h]\n",
    "            v1,v2 = EV[:,abs(edge)]\n",
    "            if sign(edge) == -1\n",
    "                v1,v2=v2,v1\n",
    "            end\n",
    "            triangles[h] = Int[pivots[k],v1,v2]\n",
    "        end\n",
    "        P = V,hcat(triangles...)\n",
    "        out[k] = Surface(P,true)\n",
    "    end\n",
    "    return out\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.6.5",
   "language": "julia",
   "name": "julia-1.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
